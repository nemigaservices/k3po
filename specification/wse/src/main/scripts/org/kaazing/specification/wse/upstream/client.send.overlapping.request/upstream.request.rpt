#
# Copyright 2007-2015, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property sequence ${wse:randomInt(100)}
property data ${wse:randomBytes(5)}

connect http://localhost:8080/path/;e/cbm?query
connected

write method "POST"
write version "HTTP/1.1"
write header host
write header content-length
write header "X-Next-Protocol" "wse/1.0"
write header "X-Sequence-No" ${wse:asString(sequence)}
write header "X-WebSocket-Version" "wseb-1.0"
write close

read status "201" /.+/
read version "HTTP/1.1"
read header "Content-Type" /text\/plain;charset=(?i)utf-8/

read /(?<upstreamUrl>http:\/\/localhost:8080\/path\/.+)\n/
read /(?<downstreamUrl>http:\/\/localhost:8080\/path\/.+)\n/
read notify CREATED
read closed

# Downstream
connect await CREATED
        ${downstreamUrl}
connected

write method "GET"
write version "HTTP/1.1"
write header host
write header "X-Next-Protocol" "wse/1.0"
write header "X-Sequence-No" ${wse:asString(sequence + 1)}
write header content-length
write close

read status "200" /.+/
read header "Content-Type" "application/octet-stream"
read header "Connection" "close"

# read data from first upstream
read [0x80 0x05] ${data}
read notify FIRST_UPSTREAM_CONNECTED

read [0x01 0x30 0x32 0xFF]
read [0x01 0x30 0x31 0xFF]
read closed


# Upstream
connect await CREATED
        ${upstreamUrl}
connected

write method "POST"
write version "HTTP/1.1"
write header host
write header "Content-Type" "application/octet-stream"
write header "Transfer-Encoding" "chunked"
write header "X-Next-Protocol" "wse/1.0"
write header "X-Sequence-No" ${wse:asString(sequence + 1)}

write [0x80 0x05] ${data}

write await SECOND_UPSTREAM_COMPLETE
write [0x01 0x30 0x31 0xFF]
write close

read status "200" /.+/
read version "HTTP/1.1"
read closed

# Overlapping Upstream
connect await FIRST_UPSTREAM_CONNECTED
        ${upstreamUrl}
connected

write method "POST"
write version "HTTP/1.1"
write header host
write header "Content-Type" "application/octet-stream"
write header "X-Next-Protocol" "wse/1.0"
write header "X-Sequence-No" ${wse:asString(sequence + 2)}
write header content-length

write [0x01 0x30 0x31 0xFF]
write close

read status /4\d\d/ /.+/
read notify SECOND_UPSTREAM_COMPLETE
read closed