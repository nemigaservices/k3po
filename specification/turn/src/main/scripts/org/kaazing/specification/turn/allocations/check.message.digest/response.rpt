#
# Copyright 2007-2015, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property realm "demo"
property nonce "adl7W7PeDU4hKE72jdaQvbAMcr6h39sm"
property acceptURI "tcp://localhost:3478"

accept ${acceptURI}
accepted
connected

##Allocation request
read [0x00] [0x03] [0x00] [0x14] #Type|MessageLength
read [0x21] [0x12] [0xa4] [0x42] #Magic cookie
# hardcode the transaction id, such that the message integrity does not change from one run to another
read [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attributes
read [0x00] [0x0d] [0x00] [0x04]
read [0x00] [0x00] [0x0e] [0x10]
#Attributes
read [0x00] [0x19] [0x00] [0x04]
read [0x11] [0x00] [0x00] [0x00]
#Attributes
read [0x00] [0x1a] [0x00] [0x00]

##401 Allocation error response
#STUN Header
write [0x01] [0x13] [0x00] [0x40] #Type|Length
write [0x21] [0x12] [0xa4] [0x42] #Magic Cookie
write [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attribute ERROR-CODE
write [0x00] [0x09] [0x00] [0x10]
write [0x00] [0x00] [0x04] [0x01]
write "Unauthorized"
#Attribute Realm
write [0x00] [0x14] [0x00] [0x04]
write ${realm}
#Attribute Nonce
write [0x00] [0x15] [0x00] [0x20]
write ${nonce}

#allocation request (w/ cred)
read [0x00] [0x03] [0x00] [0x60]
read [0x21] [0x12] [0xa4] [0x42]
read [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attribute Lifetime
read [0x00] [0x0d] [0x00] [0x04]
read [0x00] [0x00] [0x0e] [0x10] #3600 seconds
#Attribute Requested Transport
read [0x00] [0x19] [0x00] [0x01]
read [0x11] [0x00] [0x00] [0x00]
#Attribute Don't Fragment
read [0x00] [0x1a] [0x00] [0x00]
#Attribute Username
read [0x00] [0x06] [0x00] [0x03]
read "joe" [0x00]
#Attribute Realm
read [0x00] [0x14] [0x00] [0x04]
read ${realm}
#Attribute Nonce
read [0x00] [0x15] [0x00] [0x20]
read ${nonce}
#Attribute Message-Integrity
read [0x00] [0x08] [0x00] (byte:length)
read [0xdf] [0xb7] [0x77] [0x47] [0x55] [0x41] [0xdf] [0xea] [0xe6] [0xae] [0xd4] [0x9d] [0x54] [0x49] [0xc5] [0x49] [0x60] [0x76] [0xce] [0x7f]

#200 response w/ mapped address
#STUN Header
write [0x01] [0x03] [0x00] [0x38] #Type|Length
write [0x21] [0x12] [0xa4] [0x42] #Magic Cookie
write [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attribute Lifetime
write [0x00] [0x0d] [0x00] [0x04]
write [0x00] [0x00] [0x0c] [0x08] #3080 seconds
#Attribute XOR-Relayed-Address
write [0x00] [0x16] [0x00] [0x08]
write [0x00] [0x01] ${turn:portXOR(8080)} #port 8080
write ${turn:ipXOR("192.0.2.15")}
#Attribute XOR-Mapped-Address
write [0x00] [0x20] [0x00] [0x08]
write [0x00] [0x01] ${turn:portXOR(8080)} #port 8080
write ${turn:ipXOR("192.0.2.15")}
#Attribute Message-Integrity
write [0x00] [0x08] [0x00] ${length}
write [0xaa] [0xba] [0x97] [0x29] [0xaf] [0x4b] [0x5b] [0x60] [0x6d] [0xc7] [0xac] [0xe4] [0xb7] [0xb9] [0x26] [0x18] [0x16] [0x88] [0x39] [0x3d]
