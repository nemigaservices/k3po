#
# Copyright 2007-2015, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property connectURI "tcp://localhost:3478"
connect ${connectURI}
connected

##Allocation request
#STUN Message type | Message Length (***CONFIRM***)
write [0x00] [0x03] [0x00] [0x14]
#Magic Cookie (fixed required value 0x2112A442)
write [0x21] [0x12] [0xa4] [0x42]
#Transaction ID (96-bit identifier|unique)
# hardcode the transaction id, such that the message integrity does not change from one run to another
write [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attribute Lifetime (Type 2 byte|Length 2 byte|Value x byte - must end on 4 byte boundary)
write [0x00] [0x0d] [0x00] [0x04]
write [0x00] [0x00] [0x0e] [0x10] #3600 seconds
#Attribute Requested Transport (Type|Length|Value)
write [0x00] [0x19] [0x00] [0x04]
write [0x11] [0x00] [0x00] [0x00] #UDP is 17
#Attribute Don't Fragment (Type|Length|Value)
write [0x00] [0x1a] [0x00] [0x00]

#401 Allocation error response
read [0x01] [0x13] [0x00] [0x40]
read [0x21] [0x12] [0xa4] [0x42]
read [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attributes
read [0x00] [0x09] [0x00] [0x10]
read [0x00] [0x00] [0x04] [0x01]
read "Unauthorized"
#Attributes
read [0x00] [0x14] [0x00] [0x04]
read ([0..4]:realm)
#Attributes
read [0x00] [0x15] [0x00] [0x20]
read ([0..32]:nonce)

##Allocation request (w/ cred)
#STUN Message type | Message Length (***CONFIRM***)
write [0x00] [0x03] [0x00] [0x60]
#Magic Cookie
write [0x21] [0x12] [0xa4] [0x42]
#Transaction ID
write [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attribute Lifetime
write [0x00] [0x0d] [0x00] [0x04]
write [0x00] [0x00] [0x0e] [0x10] #3600 seconds
#Attribute Requested Transport
write [0x00] [0x19] [0x00] [0x01]
write [0x11] [0x00] [0x00] [0x00]
#Attribute Don't Fragment
write [0x00] [0x1a] [0x00] [0x00]
#Attribute Username
write [0x00] [0x06] [0x00] [0x03]
write "joe" [0x00]
#Attribute Realm
write [0x00] [0x14] [0x00] [0x04]
write ${realm}
#Attribute Nonce
write [0x00] [0x15] [0x00] [0x20]
write ${nonce}
#Attribute Message-Integrity
write [0x00] [0x08] [0x00] [0x14]
write ${turn:messageDigestHMACEncoding("joe:demo:welcome")}

#200 response w/ mapped address
#STUN Header
read [0x01] [0x03] [0x00] [0x38]
read [0x21] [0x12] [0xa4] [0x42]
read [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42] [0x21] [0x12] [0xa4] [0x42]
#Attribute Lifetime
read [0x00] [0x0d] [0x00] [0x04]
read [0x00] [0x00] [0x0c] [0x08]
#Attribute XOR-Relayed-Address
read [0x00] [0x16] [0x00] [0x08]
read [0x00] [0x01] ${turn:portXOR(8080)} #port 8080
read ${turn:ipXOR("192.0.2.15")}
#Attribute XOR-Mapped-Address
read [0x00] [0x20] [0x00] [0x08]
read [0x00] [0x01] ${turn:portXOR(8080)} #port 8080
read ${turn:ipXOR("192.0.2.15")}
#Attribute Message-Integrity
read [0x00] [0x08] [0x00] (byte:length)
read [0xaa] [0xba] [0x97] [0x29] [0xaf] [0x4b] [0x5b] [0x60] [0x6d] [0xc7] [0xac] [0xe4] [0xb7] [0xb9] [0x26] [0x18] [0x16] [0x88] [0x39] [0x3d]
