#
# Copyright 2007-2015, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property location "http://localhost:8000/resource"
property validCredentials ${http:loginBase64Encoder("joe:welcome")}
property validCredentials2 ${http:loginBase64Encoder("pin:1234")}
property challenge1 "Basic realm=\"firstFactor\""
property authHeader1 ${http:append("Basic ", validCredentials)}
property challenge2 "Basic realm=\"secondFactor\""
property authHeader2 ${http:append("Basic ", validCredentials2)}

connect ${location}
connected

write method "GET"
write close
read status "401" "Unauthorized"
read header "WWW-Authenticate" ${challenge1}
read notify FIRST_REQUEST_COMPLETE

connect await FIRST_REQUEST_COMPLETE
connect ${location}
connected
write method "GET"
write header "Authorization" ${authHeader1}
write close
read status "401" "Unauthorized"
read header "Sec-Challenge-Identity" /(?<secChallengeIdentity>.*)/
read header "WWW-Authenticate" ${challenge2}
read notify SECOND_REQUEST_COMPLETE

connect await SECOND_REQUEST_COMPLETE
connect ${location}
connected
write method "GET"
write header "Authorization" ${authHeader2}
write header "Sec-Challenge-Identity" ${secChallengeIdentity}
write close
read status "200" "OK"
